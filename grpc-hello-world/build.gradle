plugins {
    id 'java'
    id 'idea'
    alias(libs.plugins.protobuf)
}

group 'io.github.aaronchenwei.example'
version '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.grpcProtobuf)
    implementation(libs.grpcStub)
    runtimeOnly(libs.grpcNetty)

    compileOnly(libs.javaxAnnotationApi)

    implementation(libs.slf4jApi)
    runtimeOnly(libs.logbackClassic)

    testImplementation(libs.junitJupiterApi)
    testRuntimeOnly(libs.junitJupiterEngine)
}

protobuf {
    protoc {
        artifact = libs.protoc.get().toString()
    }
    plugins {
        grpc {
            artifact = libs.protocGenGrpcJava.get().toString()
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}
