plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.9.1'
}

group 'io.github.aaronchenwei.example'
version '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.projectreactor:reactor-core:3.4.24'

    implementation 'io.grpc:grpc-protobuf:1.49.2'
    implementation 'io.grpc:grpc-stub:1.50.2'
    runtimeOnly 'io.grpc:grpc-netty:1.50.2'

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'

    implementation 'org.slf4j:slf4j-api:2.0.3'
    runtimeOnly 'ch.qos.logback:logback-classic:1.4.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.8'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.50.2'
        }
        rxgrpc {
            artifact = 'com.salesforce.servicelibs:reactor-grpc:1.2.3'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            rxgrpc {}
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}
