plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.9.1'
}

group 'io.github.aaronchenwei.example'
version '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

ext {
    version_grpc = '1.50.0'
    version_reactor_grpc = '1.2.3'
    version_protoc = '3.21.7'
    version_slf4j = '2.0.3'
    version_logback = '1.4.4'
    version_junit = '5.9.1'
    version_reactor = '3.4.23'
}

dependencies {
    implementation "io.projectreactor:reactor-core:${version_reactor}"

    implementation "io.grpc:grpc-protobuf:${version_grpc}"
    implementation "io.grpc:grpc-stub:${version_grpc}"

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    runtimeOnly "io.grpc:grpc-netty-shaded:${version_grpc}"

    implementation "com.salesforce.servicelibs:reactor-grpc-stub:${version_reactor_grpc}"

    implementation "org.slf4j:slf4j-api:${version_slf4j}"
    runtimeOnly "ch.qos.logback:logback-classic:${version_logback}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${version_junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${version_junit}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${version_protoc}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${version_grpc}"
        }
        rxgrpc {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${version_reactor_grpc}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            rxgrpc {}
        }
    }
}

test {
    useJUnitPlatform()
}
